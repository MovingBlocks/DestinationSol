apply from: '../config/gradle/common.gradle'
apply plugin: 'distribution'

project.ext.mainClassName = "org.destinationsol.desktop.SolDesktop"

dependencies {
    compile project(":engine")
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-controllers-desktop:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-controllers-platform:$gdxVersion:natives-desktop"

    compile group: 'org.terasology.crashreporter', name: 'cr-destsol', version: '4.0.0'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'
}

task run(type: JavaExec) {
    dependsOn classes

    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.rootProject.rootDir
    ignoreExitValue = true
}

jar {
    archiveName = "solDesktop.jar"

    manifest {
        def manifestClasspath = configurations.runtime.collect { it.getName() }.join(" ")
        attributes 'Main-Class': project.mainClassName
        attributes 'Class-Path': manifestClasspath
    }
}

distributions {
    main {
        baseName = "DestinationSol"
        // set the name to DestinationSol.zip or tar by hiding the version
        version = ""

        contents {
            from("$rootDir/launcher")
            from("$rootDir/modules") {
                into 'modules/'
            }
            into('libs') {
                from configurations.runtime
                from jar
            }
        }
    }
}

// TODO: LibGDX Generated config for Eclipse. Needs adjustment for assets not being in the Android facade
eclipse {
    project {
        name = appName + "-desktop"
        linkedResource name: 'assets', type: '2', location: 'PARENT-1-PROJECT_LOC/android/assets'
    }
    // Hack to allow the game to run via eclipse. See #209 for more information.
    classpath {
        file {
            withXml { xml ->
                def node = xml.asNode()
                node.appendNode('classpathentry', [kind: 'src', path: '/engine' ])
            }
            whenMerged {
                entries.removeAll { it.kind == 'src' && it.path == '/DestinationSol-main' }
            }
        }
    }
}

task afterEclipseImport(description: "Post processing after project generation", group: "IDE") {
    doLast {
        def classpath = new XmlParser().parse(file(".classpath"))
        new Node(classpath, "classpathentry", [kind: 'src', path: 'assets']);
        def writer = new FileWriter(file(".classpath"))
        def printer = new XmlNodePrinter(new PrintWriter(writer))
        printer.setPreserveWhitespace(true)
        printer.print(classpath)
    }
}

tasks.withType(JavaExec) {
    if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=9099'
    }
}
