apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'project-report'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'com.github.spotbugs'
apply plugin: 'jacoco'

dependencies {
    pmd('net.sourceforge.pmd:pmd-core:6.15.0')
    pmd('net.sourceforge.pmd:pmd-java:6.15.0')
    // the FindBugs version is set in the configuration
}

version = '2.0.1'
ext {
    appName = 'DestinationSol'
    gdxVersion = '1.9.8'
    roboVMVersion = '2.3.3'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {

    // Good ole Maven central
    mavenCentral()

    // Repos for LibGDX
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }

    // Terasology Artifactory for any shared libs
    maven { url "http://artifactory.terasology.org/artifactory/virtual-repo-live" }

    //everit-org JSON schema dependency
    maven { url "https://jitpack.io" }

    maven { url "https://maven.google.com" }

    //This gets the java SemVer library that Gestalt uses
    maven { url = 'https://heisluft.de/maven/' }
}

test {
    // ignoreFailures: Specifies whether the build should break when the verifications performed by this task fail.
    ignoreFailures = true

    // showStandardStreams: makes the standard streams (err and out) visible at console when running tests
    testLogging.showStandardStreams = true

    // Arguments to include while running tests
    jvmArgs '-Xms512m', '-Xmx1024m'
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    dependsOn test // Despite doc saying this should be automatic we need to explicitly add it anyway :-(
    reports {
        // We only use the .exec report for display in Jenkins and such. More could be enabled if desired.
        xml.enabled false
        csv.enabled false
        html.enabled false
    }
}

// The config files here work in both a multi-project workspace (IDEs, running from source) and for solo module builds
// Solo module builds in Jenkins get a copy of the config dir from the engine harness so it still lives at root/config
// TODO: Maybe update other projects like modules to pull the zipped dependency so fewer quirks are needed in Jenkins
checkstyle {
    ignoreFailures = true
    configFile = new File(rootDir, 'config/metrics/checkstyle/checkstyle.xml')
    configProperties.samedir = checkstyle.configFile.parentFile
}

pmd {
    ignoreFailures = true
    ruleSetFiles = files("$rootDir/config/metrics/pmd/pmd.xml")
    // By default, gradle uses both ruleset file AND the rulesets. Override the ruleSets to use only those from the file
    ruleSets = []
}

spotbugs {
    toolVersion = '4.0.0'
    ignoreFailures = true
    excludeFilter = new File(rootDir, "config/metrics/findbugs/findbugs-exclude.xml")
}

spotbugsMain {
    reports {
        xml {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.xml")
        }
    }
}

// TODO: Temporary until javadoc has been fixed for Java 8 everywhere
javadoc {
    failOnError = false
}

tasks.withType(Checkstyle) {
    group = 'Reporting'
}

tasks.withType(Pmd) {
    group = 'Reporting'
}

// Make sure our config file for code analytics get extracted (vulnerability: non-IDE execution of single analytic)
ideaModule.dependsOn rootProject.extractConfig
tasks.eclipse.dependsOn rootProject.extractConfig
check.dependsOn rootProject.extractConfig
